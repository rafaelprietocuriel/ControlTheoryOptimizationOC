Type
standardmodel

Modelname
covid2region

Description
Modeling two region Covid with decreasing immunity

Variable
state::SA,IA,RA,TA,SB,IB,RB,TB
control::u,vA,vB

Statedynamics
ode::DSA=nu-SA*(betaA*IA/(SA+IA+RA)+betaBA*(1-u)^2*IB/(SA+IA+RA)+betaAB*(1-u)^2*IB/(SB+IB+RB))-mu*SA+eff*(phi0+phi1*TA/(RA+tau))*RA-epsilon*vA
ode::DIA=SA*(betaA*IA/(SA+IA+RA)+betaBA*(1-u)^2*IB/(SA+IA+RA)+betaAB*(1-u)^2*IB/(SB+IB+RB))-(alpha+mu+muI)*IA+(1-eff)*(phi0+phi1*TA/(RA+tau))*RA
ode::DRA=alpha*IA-mu*RA-(phi0+phi1*TA/(RA+tau))*RA+epsilon*vA
ode::DTA=RA-(phi0+phi1*TA/(RA+tau))*TA+kappa*IA
ode::DSB=nu-SB*(betaB*IB/(SB+IB+RB)+betaAB*(1-u)^2*IA/(SB+IB+RB)+betaBA*(1-u)^2*IA/(SA+IA+RA))-mu*SB+eff*(phi0+phi1*TB/(RB+tau))*RB-epsilon*vB
ode::DIB=SB*(betaB*IB/(SB+IB+RB)+betaAB*(1-u)^2*IA/(SB+IB+RB)+betaBA*(1-u)^2*IA/(SA+IA+RA))-(alpha+mu+muI)*IB+(1-eff)*(phi0+phi1*TB/(RB+tau))*RB
ode::DRB=alpha*IB-mu*RB-(phi0+phi1*TB/(RB+tau))*RB+epsilon*vB
ode::DTB=RB-(phi0+phi1*TB/(RB+tau))*TB+kappa*IB

Objective
int::-MA*Mfac*(bA1*IA+bA2*IA^2)-MB*Mfac*(bB1*IB+bB2*IB^2)-g1*u-g2*u^2-m1*vA-m2*vA^2-m1*vB-m2*vB^2

Controlconstraint
CC1::ineq::u>=umin
CC2::ineq::u<=umax
CC3::ineq::vA>=vmin
CC4::ineq::vA<=vAmax*vfac
CC5::ineq::vB>=vmin
CC6::ineq::vB<=vBmax*vfac

Stateconstraint
SC1::ineq::SA>=0,1
SC2::ineq::SB>=0,1

ArcDefinition
0::CC4,CC6
1::CC1
2::CC4,CC6,CC1
3::SC1,CC6
4::SC1,CC1,CC6
5::SC2,CC4
6::SC2,CC1,CC4
7::SC1,SC2,CC1
%5::SC1,CC1,CC5
% 6::SC2,CC3
% 7::SC2,CC1,CC3
% 8::SC1,SC2,CC1

State % define the state variable(s) that are explicitly derived from the state constraints 
3,4::SA
5,6::SB
7::SA,SB

Parameter
r::0.0
alpha::1/15
betaA::0
betaB::0
betaAB::1/15
betaBA::1/15
nu::1/1000
mu::1/1000
muI::0
bA1::1e-3
bA2::0
bB1::1e-3
bB2::0
Mfac::1
MA::1000
MB::1000
phi0::0.001
phi1::0
g1::0
g2::1000
m1::0
m2::0
vmin::0
vAmax::1e-4
vBmax::1e-4
vfac::1
umin::0
umax::1
epsilon::1
eff::1
kappa::0
tau::1e-5 % for numerical purposes


% JA=SA*(betaA*IA/(SA+IA+RA)+betaBA*(1-u)^2*IB/(SA+IA+RA)+betaAB*(1-u)^2*IB/(SB+IB+RB))

%JB=SB*(betaB*IB/(SB+IB+RB)+betaAB*(1-u)^2*IA/(SB+IB+RB)+betaBA*(1-u)^2*IA/(SA+IA+RA))