Type
standardmodel

Modelname
seqijr

Description
Optimal and sub-optimal quarantine and isolation control in SARS epidemics, yanzou2008

Variable
state::S,E,Q,I,J,R
control::u1,u2

Statedynamics
ode::DS=L+kappa*mu*(S+E+Q+I+J+R)-S*(beta*I+epsilonE*E*beta+epsilonQ*beta*Q+epsilonJ*beta*J)/(S+E+Q+I+J+R)-mu*S
ode::DE=p+S*(beta*I+epsilonE*E*beta+epsilonQ*beta*Q+epsilonJ*beta*J)/(S+E+Q+I+J+R)-(u1+k1+mu)*E
ode::DQ=u1*E-(k2+mu)*Q
ode::DI=k1*E-(u2+d1+sigma1+mu)*I
ode::DJ=u2*I+k2*Q-(d2+sigma2+mu)*J
ode::DR=sigma1*I+sigma2*J-mu*R


Salvagevalue
func::-epsilon*S1*E-epsilon*S2*Q-epsilon*S3*I-epsilon*S4*J

Objective
int::-B1*E-B2*Q-B3*I-B4*J-C1/2*u1^2-C2/2*u2^2


Controlconstraint % identifier has to contain an alphabetic character 
CC1::ineq::u1>=u1min
CC2::ineq::u1<=u1max
CC3::ineq::u2>=u2min
CC4::ineq::u2<=u2max

ArcDefinition
0::[]
1::CC1
2::CC2
3::CC3
4::CC4
5::CC1,CC3
6::CC2,CC3
7::CC1,CC4
8::CC2,CC4

Parameter
r::0
L::0
kappa::1
beta::0.2
epsilonE::0.3
epsilonQ::0
epsilonJ::0.1
mu::0.000034
p::0
k1::0.1
k2::0.125
d1::0.0079
d2::0.0068
sigma1::0.0337
sigma2::0.0386
B1::1
B2::1
B3::1
B4::1
C1::300
C2::600
u1min::0.05
u1max::0.5
u2min::0.05
u2max::0.5
epsilon::0
S1::1
S2::1
S3::1
S4::1

% S: Susceptible individuals;
% E: Asymptomatic individuals who have been exposed to the virus but have not yet developed clinical symptoms of SARS
% Q: Quarantined individuals;
% I: Symptomatic individuals;
% J: Isolated individuals;
% R: Recovered individuals; 