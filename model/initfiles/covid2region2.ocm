Type
standardmodel

Modelname
covid2region2

Description
Modeling two region Covid with decreasing immunity

Variable
state::SA,IA,RA,TA,SB,IB,RB,TB
control::u,vA,vB

%
Statedynamics
ode::DSA=nu-SA*(betaA*IA+betaBA*(1-u)*IB+betaAB*(1-u)*IB)-mu*SA+eff*(phi0+phi1*TA/(RA+tau)*(1+kappa*(IA+IB)))*RA-facA/tanh(zeta)*tanh(zeta*SA)*vA
ode::DIA=SA*(betaA*IA+betaBA*(1-u)*IB+betaAB*(1-u)*IB)-(alpha+mu+muI)*IA+(1-eff)*(phi0+phi1*TA/(RA+tau)*(1+kappa*(IA+IB)))*RA
ode::DRA=alpha*IA-mu*RA-(phi0+phi1*TA/(RA+tau)*(1+kappa*(IA+IB)))*RA+facA/tanh(zeta)*tanh(zeta*SA)*vA
ode::DTA=RA-(phi0+phi1*TA/(RA+tau)*(1+kappa*(IA+IB)))*TA+kappa*IA
ode::DSB=nu-SB*(betaB*IB+betaAB*(1-u)*IA+betaBA*(1-u)*IA)-mu*SB+eff*(phi0+phi1*TB/(RB+tau)*(1+kappa*(IA+IB)))*RB-facB/tanh(zeta)*tanh(zeta*SB)*vB
ode::DIB=SB*(betaB*IB+betaAB*(1-u)*IA+betaBA*(1-u)*IA)-(alpha+mu+muI)*IB+(1-eff)*(phi0+phi1*TB/(RB+tau)*(1+kappa*(IA+IB)))*RB
ode::DRB=alpha*IB-mu*RB-(phi0+phi1*TB/(RB+tau)*(1+kappa*(IA+IB)))*RB+facB/tanh(zeta)*tanh(zeta*SB)*vB
ode::DTB=RB-(phi0+phi1*TB/(RB+tau)*(1+kappa*(IA+IB)))*TB

Objective
int::-MA*Mfac*(bA1*IA+bA2*IA^2)-MB*Mfac*(bB1*IB+bB2*IB^2)-g1*u-g2*u^2-m1A*vA-m2A*vA^2-m1B*vB-m2B*vB^2

Controlconstraint
CC1::ineq::u>=umin
CC2::ineq::u<=umax
CC3::ineq::vA>=vmin
CC4::ineq::vB>=vmin
CC5::ineq::vA+vB<=vmax

ArcDefinition
0::[]
1::CC1
2::CC3
3::CC1,CC3
4::CC5
5::CC1,CC5
6::CC4
7::CC1,CC4
8::CC5,CC4
9::CC1,CC5,CC4
10::CC3,CC5
11::CC1,CC3,CC5
12::CC3,CC4
13::CC2,CC3
14::CC2,CC5
15::CC2,CC4
16::CC2,CC4,CC5
17::CC2,CC3,CC5
%6::SC2,CC3
%7::SC2,CC1,CC3

Parameter
r::0.0
alpha::1/15
betaA::0
betaB::0
betaAB::1/15
betaBA::1/15
nu::1/1000
mu::1/1000
muI::0
bA1::1e-3
bA2::0
bB1::1e-3
bB2::0
Mfac::1
MA::1000
MB::1000
phi0::0.001
phi1::2.5309e-5
g1::0
g2::1
m1A::0
m2A::0
m1B::0
m2B::1
vmin::0
vmax::1e-4
umin::0
umax::1
eff::1
kappa::0
facA::1
facB::1
tau::1e-5 % for numerical purposes
zeta::5e3 % for numerical purposes


% JA=SA*(betaA*IA/(SA+IA+RA)+betaBA*(1-u)*IB/(SA+IA+RA)+betaAB*(1-u)*IB/(SB+IB+RB))

%JB=SB*(betaB*IB/(SB+IB+RB)+betaAB*(1-u)*IA/(SB+IB+RB)+betaBA*(1-u)*IA/(SA+IA+RA))
