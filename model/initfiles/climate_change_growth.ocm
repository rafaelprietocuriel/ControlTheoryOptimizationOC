Type
standardmodel

Description
model greineretal2010 

Modelname
climate_change_growth

Variable
state::K,T,M
control::C

Statedynamics
ode::DK=B*K^alpha*(a1*(T-T0)^2+1)^(-psi)-C-A-(delta+n)*K
ode::DT=(Q*(k1*2/P*atan(P*(T-293)/2)+k2)/4-epsilon*sigmaT*T^4*(1-f)+(1-xi)*6.3*log(M/M0))/ch
ode::DM=beta1*(a*K/A)^gamma-mu*M

Objective
expdisc::(rho-n)
int::L0*log(C)

Controlconstraint % identifier has to contain an alphabetic character
CC::ineq::C>=Clow

% contains all combinations of constraints that have to be considered
% Syntax: arcid::c1 c2
% c1 and c2 are the identifiers of constraints defined before, [] stands
% for the unconstrained case.
% underscores indicate that multiple solutions of the maximizing
% Hamiltonian exist for a specific constraint combination 
ArcDefinition
0::[]
1::CC


Parameter
rho::0.035
n::0.03
delta::0.075
A::0.0012
B::1
alpha::0.18	
beta1::0.49
mu::0.1
gamma::1
xi::0.23
a::3.5e-4
a1::0.025
psi::0.025
k1::5.6e-3
k2::0.2135
epsilon::0.95
sigmaT::5.67e-8
f::88/109
Q::1367.5
ch::0.1497
P::3.18445
M0::1
L0::1
T0::288
Clow::0